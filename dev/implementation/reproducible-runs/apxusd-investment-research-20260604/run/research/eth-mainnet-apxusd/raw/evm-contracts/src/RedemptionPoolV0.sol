// SPDX-License-Identifier: MIT
pragma solidity 0.8.30;

import {AccessManaged} from "@openzeppelin/contracts/access/manager/AccessManaged.sol";
import {Pausable} from "@openzeppelin/contracts/utils/Pausable.sol";
import {ReentrancyGuardTransient} from "@openzeppelin/contracts/utils/ReentrancyGuardTransient.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {IERC20Metadata} from "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {ERC20Burnable} from "@openzeppelin/contracts/token/ERC20/extensions/ERC20Burnable.sol";

import {IRedemptionPool} from "./interfaces/IRedemptionPool.sol";

/**
 * @title RedemptionPoolV0
 * @notice Redeems asset tokens for reserve assets at a configurable exchange rate
 * @dev Non-upgradeable. Uses AccessManager for role-based access; ROLE_REDEEMER for redeem(), ADMIN for
 *      deposit/withdraw/setExchangeRate/pause/unpause. Exchange rate is reserve asset per asset (1e18 = 1:1)
 *      in the precision of the asset token.
 *
 * @dev The asset MUST support burnFrom(address,uint256) as defined in ERC20Burnable.
 */
contract RedemptionPoolV0 is IRedemptionPool, AccessManaged, Pausable, ReentrancyGuardTransient {
    using SafeERC20 for IERC20;

    /// @notice Asset token to be redeemed (burned); e.g. apxUSD
    ERC20Burnable public immutable asset;
    /// @notice Reserve asset paid out on redemption; e.g. USDC
    IERC20 public immutable reserveAsset;
    /// @notice True if asset has more decimals than reserve, false otherwise
    bool private immutable assetHasMoreDecimals;
    /// @notice Scaling factor for decimal conversion: 10^abs(assetDecimals - reserveDecimals)
    uint256 private immutable decimalScalingFactor;
    /// @notice Exchange rate: reserve asset per asset, 1e18 = 1:1 (reserveAmount = assetsAmount * exchangeRate / 1e18)
    uint256 public exchangeRate;

    /**
     * @notice Initializes the redemption pool
     * @param initialAuthority Address of the AccessManager contract
     * @param asset_ Asset token (e.g. apxUSD)
     * @param reserveAsset_ Reserve asset token (e.g. USDC)
     */
    constructor(address initialAuthority, ERC20Burnable asset_, IERC20 reserveAsset_) AccessManaged(initialAuthority) {
        if (initialAuthority == address(0)) revert InvalidAddress("initialAuthority");
        if (address(asset_) == address(0)) revert InvalidAddress("asset");
        if (address(reserveAsset_) == address(0)) revert InvalidAddress("reserveAsset");

        asset = asset_;
        reserveAsset = reserveAsset_;
        exchangeRate = 1e18;

        // Cache decimal scaling information
        uint8 assetDecimals = IERC20Metadata(address(asset_)).decimals();
        uint8 reserveDecimals = IERC20Metadata(address(reserveAsset_)).decimals();

        if (assetDecimals >= reserveDecimals) {
            assetHasMoreDecimals = true;
            decimalScalingFactor = 10 ** (assetDecimals - reserveDecimals);
        } else {
            assetHasMoreDecimals = false;
            decimalScalingFactor = 10 ** (reserveDecimals - assetDecimals);
        }
    }

    // ============ Core Functions ============

    /// @inheritdoc IRedemptionPool
    /// @dev Does not consider pause state or reserve balance; callers should check paused() and reserveBalance()
    ///      Rounding is downward in favor of the pool.
    ///      Formula: reserveAmount = (assetsAmount * exchangeRate) / (1e18 * 10^(assetDecimals - reserveDecimals))
    function previewRedeem(uint256 assetsAmount) public view override returns (uint256 reserveAmount) {
        if (assetHasMoreDecimals) {
            // Scale down if asset has more decimals
            return (assetsAmount * exchangeRate) / (1e18 * decimalScalingFactor);
        } else {
            // Scale up if reserve has more decimals
            return (assetsAmount * exchangeRate * decimalScalingFactor) / 1e18;
        }
    }

    /// @inheritdoc IRedemptionPool
    function redeem(uint256 assetsAmount, address receiver, uint256 minReserveAssetOut)
        external
        override
        restricted
        whenNotPaused
        nonReentrant
        returns (uint256 reserveAmount)
    {
        if (assetsAmount == 0) revert InvalidAmount("assetsAmount", assetsAmount);
        if (receiver == address(0)) revert InvalidAddress("receiver");

        reserveAmount = previewRedeem(assetsAmount);
        if (reserveAmount < minReserveAssetOut) {
            revert SlippageExceeded(reserveAmount, minReserveAssetOut);
        }
        uint256 balance = reserveBalance();
        if (reserveAmount > balance) {
            revert InsufficientBalance(address(this), balance, reserveAmount);
        }
        // Burn the asset and transfer out the reserve asset
        asset.burnFrom(msg.sender, assetsAmount);
        reserveAsset.safeTransfer(receiver, reserveAmount);

        emit Redeemed(msg.sender, assetsAmount, reserveAmount);
    }

    // ============ Admin Functions ============

    /// @inheritdoc IRedemptionPool
    function deposit(uint256 reserveAmount) external override restricted nonReentrant {
        if (reserveAmount == 0) revert InvalidAmount("reserveAmount", reserveAmount);
        reserveAsset.safeTransferFrom(msg.sender, address(this), reserveAmount);

        emit ReservesDeposited(msg.sender, reserveAmount);
    }

    /// @inheritdoc IRedemptionPool
    function withdraw(uint256 amount, address receiver) external override restricted {
        withdrawTokens(address(reserveAsset), amount, receiver);
    }

    /// @inheritdoc IRedemptionPool
    /// @dev Use to recover tokens erroneously sent to the pool (e.g. asset or any other ERC20).
    ///      Can also withdraw reserve asset; equivalent to withdraw() for that case.
    function withdrawTokens(address withdrawAsset, uint256 amount, address receiver)
        public
        override
        restricted
        nonReentrant
    {
        if (amount == 0) revert InvalidAmount("amount", amount);
        if (receiver == address(0)) revert InvalidAddress("receiver");

        uint256 balance = IERC20(withdrawAsset).balanceOf(address(this));
        if (amount > balance) revert InsufficientBalance(address(this), balance, amount);
        IERC20(withdrawAsset).safeTransfer(receiver, amount);

        emit Withdraw(msg.sender, withdrawAsset, amount, receiver);
    }

    /// @inheritdoc IRedemptionPool
    /// @param newRate Reserve asset per asset, 1e18 = 1:1
    function setExchangeRate(uint256 newRate) external override restricted {
        if (newRate == 0) revert InvalidAmount("newRate", newRate);

        uint256 oldRate = exchangeRate;
        exchangeRate = newRate;

        emit ExchangeRateUpdated(oldRate, newRate);
    }

    /// @notice Pause redemptions
    /// @dev Restricted to admin role
    function pause() external restricted {
        _pause();
    }

    /// @notice Unpause redemptions
    /// @dev Restricted to admin role
    function unpause() external restricted {
        _unpause();
    }

    // ============ View Functions ============

    /// @inheritdoc IRedemptionPool
    function reserveBalance() public view override returns (uint256) {
        return reserveAsset.balanceOf(address(this));
    }
}
